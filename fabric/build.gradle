plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-fabric:${architectury_api_version}"

    // Create Mod
    modImplementation("com.simibubi.create:create-fabric:${create_fabric_version}-mc${minecraft_version}")
    modCompileOnly("dev.engine-room.flywheel:flywheel-fabric-api-${minecraft_version}:${flywheel_fabric_version}")
    modImplementation("net.createmod.ponder:Ponder-Fabric-${minecraft_version}:${ponder_forge_version}")

    // Create Power grid
    modImplementation "maven.modrinth:power-grid:${powergrid_version}"

    // QoL
    modCompileOnly "mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${rei_version}"
    modLocalRuntime "dev.engine-room.flywheel:flywheel-fabric-${minecraft_version}:${flywheel_fabric_version}"
    modLocalRuntime "mezz.jei:jei-${minecraft_version}-fabric:${jei_version}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    def resourceTokens = [
        version: project.version,
        forge_version_range: project.forge_version_range,
        minecraft_version: project.minecraft_version,
        architectury_api_version: project.architectury_api_version,
        create_fabric_version: project.create_fabric_version,
        powergrid_version: project.powergrid_version,
        powergrid_version_range_fabric: project.powergrid_version_range_fabric
    ]
    inputs.properties(resourceTokens)

    filesMatching('fabric.mod.json') {
        expand resourceTokens
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
