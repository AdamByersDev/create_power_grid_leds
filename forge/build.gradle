plugins {
    id 'com.gradleup.shadow'
}

loom {
    forge {
        mixinConfig "create_power_grid_leds.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    compileOnly("io.github.llamalad7:mixinextras-common:${mixin_extras_version}")
    annotationProcessor(implementation(include("io.github.llamalad7:mixinextras-forge:${mixin_extras_version}")))

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-forge:${architectury_api_version}"

    // Create Mod
    modImplementation("com.simibubi.create:create-${minecraft_version}:${create_forge_version}:slim") { transitive false }
    modImplementation("net.createmod.ponder:Ponder-Forge-${minecraft_version}:${ponder_forge_version}")
    modCompileOnly("dev.engine-room.flywheel:flywheel-forge-api-${minecraft_version}:${flywheel_forge_version}")
    modImplementation("com.tterrag.registrate:Registrate:${registrate_forge_version}")

    // Create Power grid
    modImplementation "maven.modrinth:power-grid:${powergrid_forge_version}"
    
    // QoL
    modCompileOnly "mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-forge:${rei_version}"
    modLocalRuntime("dev.engine-room.flywheel:flywheel-forge-${minecraft_version}:${flywheel_forge_version}")
    modLocalRuntime("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')
}

processResources {
    def resourceTokens = [
        version: project.version,
        forge_version_range: project.forge_version_range,
        minecraft_version: project.minecraft_version,
        architectury_api_version: project.architectury_api_version,
        create_forge_version: project.create_forge_version,
        powergrid_version: project.powergrid_version
    ]
    inputs.properties(resourceTokens)

    filesMatching('META-INF/mods.toml') {
        expand resourceTokens
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
